plugins {
    id 'base'
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "org.sonarqube" version "3.3"
}

description = """ SpringBoot OCPP 1.6 JSON """

ext {
    springVersion = '5.3.9'
    springBootVersion = '2.5.4'
    lombokVersion = '1.18.20'
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'SpringOCPP16J'
        property 'sonar.login', 'c1341dbd968034424de9254b2300adc1daea4104'
        property 'sonar.host.url', 'http://localhost:9000'
    }
}

allprojects {
    group = 'ge.devel.ocpp'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'idea'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    sourceCompatibility = 11
    targetCompatibility = 11

    dependencies {
        constraints{
            implementation("org.apache.logging.log4j:log4j-core")
            implementation("org.apache.logging.log4j:log4j-api"){
                version {
                    strictly("[2.17, 3[")
                    prefer("2.17.1")
                }
                because("CVE-2021-44228: Log4j vulnerable to remote code execution RCE")
            }
        }

        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        implementation "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation 'junit:junit:4.12'
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        jvmArgs '--enable-preview'
    }
}

project('app') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation project(':ws:ws-impl')

        implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    }

    jar {
        manifest {
            attributes 'Main-Class': "ge.devel.ocpp.SpringOcpp16JApplication"
        }
    }
}

project('ws:ws-api') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        implementation group: 'eu.chargetime.ocpp', name: 'common', version: '1.0'
        implementation group: 'eu.chargetime.ocpp', name: 'v1_6', version: '1.0.1'
        implementation group: 'eu.chargetime.ocpp', name: 'OCPP-J', version: '1.0.1'
    }
}

project('ws:ws-impl') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        implementation project(':ws:ws-api')

        implementation "org.springframework:spring-context:${springVersion}"

        implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

        implementation group: 'eu.chargetime.ocpp', name: 'common', version: '1.0'
        implementation group: 'eu.chargetime.ocpp', name: 'v1_6', version: '1.0.1'
        implementation group: 'eu.chargetime.ocpp', name: 'OCPP-J', version: '1.0.1'

        testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    }
}
